apply plugin: 'com.android.application'

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())
def AVOSCloud_App_Id = properties.getProperty('AVOSCloud.AppId')
def AVOSCloud_App_Key = properties.getProperty('AVOSCloud.AppKey')

android {
    compileSdkVersion 23
    buildToolsVersion '23.0.1'

    defaultConfig {
        applicationId "com.lulee007.xitu"
        minSdkVersion 16
        targetSdkVersion 23
        versionCode 1
        versionName "1.0"

        buildConfigField "String", "AVOSCloud_App_Id", "\"${AVOSCloud_App_Id}\""
        buildConfigField "String", "AVOSCloud_App_Key", "\"${AVOSCloud_App_Key}\""

        // Warning:Renderscript support mode is not currently supported with renderscript target 21+
//        renderscriptTargetApi 20
//        renderscriptSupportModeEnabled true
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    packagingOptions {
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/LICENSE.txt'

    }

    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            def outputFile = output.outputFile
            if (outputFile != null && outputFile.name.endsWith('.apk')) {
                def versionName = defaultConfig.versionName.replace(".", "_")
                def fileName = outputFile.name.replace(".apk", "-v${versionName}.apk")
                fileName = fileName.replace("app", "xt")
                println "[FileName]: ${fileName}"
                if (variant.buildType.name.equals('release')) {
                    output.outputFile = file("${outputFile.parent}/release/${defaultConfig.versionName}/${fileName}")
                } else {
                    output.outputFile = file("${outputFile.parent}/${fileName}")
                }
            }
        }
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:1.10.19'
    androidTestCompile 'org.hamcrest:hamcrest-library:1.1'
    // LeakCanary
    debugCompile 'com.squareup.leakcanary:leakcanary-android:1.3.1'
    releaseCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.3.1'
    compile 'com.android.support:appcompat-v7:23.1.1'
    compile 'com.android.support:design:23.1.1'
    compile 'com.android.support:cardview-v7:23.1.1'
    compile 'com.android.support:support-v4:23.1.1'
    compile 'com.squareup.okhttp:okhttp:2.7.0'
    //retrofit
    compile 'com.squareup.retrofit:retrofit:1.9.0'
    compile 'com.orhanobut:logger:1.11'
    compile 'io.reactivex:rxandroid:1.0.1'
    compile 'com.jakewharton.rxbinding:rxbinding:0.3.0'
    //An image loading and caching library for Android focused on smooth scrolling
    compile 'com.github.bumptech.glide:glide:3.6.1'
    compile 'io.reactivex:rxjava:1.0.14'
    compile 'info.android15.nucleus:nucleus:2.0.1'
    compile 'com.marshalchen.ultimaterecyclerview:library:0.3.18'
    compile('com.mikepenz:materialdrawer:4.6.2@aar') {
        transitive = true
    }
    //    compile 'com.yqritc:recyclerview-flexibledivider:1.2.6'
    //    compile 'jp.wasabeef:glide-transformations:1.3.1'
}
